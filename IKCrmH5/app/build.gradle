apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
//greendao数据库
apply plugin: 'org.greenrobot.greendao'
//听云
apply plugin: 'newlens'
//神策
apply plugin: 'com.sensorsdata.analytics.android'

android {
    signingConfigs {
        config {
            keyAlias 'wwjakcrm'
            keyPassword '123uio098'
            storeFile file('../wwjakcrm.jks')
            storePassword '123uio098'
            //去掉7.0系统v2模式的签名方式
            v2SigningEnabled false
        }
    }

    compileSdkVersion rootProject.ext.compileSdkVersion
    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion

        signingConfig signingConfigs.config

        ndk {
            abiFilters "armeabi-v7a", "arm64-v8a"
        }

        resConfigs "zh"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config

            //不显示Log
            buildConfigField "boolean", "LOG_DEBUG", "false"
        }

        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config

            //显示Log
            buildConfigField "boolean", "LOG_DEBUG", "true"
        }
    }

    productFlavors {
        lxyun {
            applicationId rootProject.ext.LX_APP_ID
            versionCode rootProject.ext.LX_VERSION_CODE
            versionName rootProject.ext.LX_VERSION_NAME
            manifestPlaceholders = [
                    PACKAGE_NAMES   : applicationId,
                    GETUI_APP_ID    : rootProject.ext.LX_GETUI_APP_ID,
                    GETUI_APP_KEY   : rootProject.ext.LX_GETUI_APP_KEY,
                    GETUI_APP_SECRET: rootProject.ext.LX_GETUI_APP_SECRET,
                    HW_PUSH_ID      : rootProject.ext.LX_HW_PUSH_ID,
                    GD_MAP_KEY      : rootProject.ext.LX_GD_MAP_KEY,
                    UM_APP_KEY      : "5cef51df4ca35742c50006b7",
                    TY_URL_SCHEME   : rootProject.ext.LX_TY_URL_SCHEME,
                    qqappid         : "1109201785"
            ]
            //环境判断
            buildConfigField "boolean", "IS_LX", "true"
            //小米推送配置
            buildConfigField "String", "XM_PUSH_KEY", "\"${rootProject.ext.LX_XM_PUSH_KEY}\""
            buildConfigField "String", "XM_PUSH_ID", "\"${rootProject.ext.LX_XM_PUSH_ID}\""
            //听云配置
            buildConfigField "String", "TY_APP_KEY", "\"${rootProject.ext.LX_TY_APP_KEY}\""
            buildConfigField "String", "APP_NAME_CN", "\"励销云\""
        }

        ikyun {
            applicationId rootProject.ext.IK_APP_ID
            versionCode rootProject.ext.IK_VERSION_CODE
            versionName rootProject.ext.IK_VERSION_NAME
            manifestPlaceholders = [
                    PACKAGE_NAMES   : applicationId,
                    GETUI_APP_ID    : rootProject.ext.IK_GETUI_APP_ID,
                    GETUI_APP_KEY   : rootProject.ext.IK_GETUI_APP_KEY,
                    GETUI_APP_SECRET: rootProject.ext.IK_GETUI_APP_SECRET,
                    HW_PUSH_ID      : rootProject.ext.IK_HW_PUSH_ID,
                    GD_MAP_KEY      : rootProject.ext.IK_GD_MAP_KEY,
                    UM_APP_KEY      : "5cef51df4ca35742c50006b7",
                    TY_URL_SCHEME   : rootProject.ext.IK_TY_URL_SCHEME,
                    qqappid         : "1109201785"
            ]
            //环境判断
            buildConfigField "boolean", "IS_LX", "false"
            //小米推送配置
            buildConfigField "String", "XM_PUSH_KEY", "\"${rootProject.ext.IK_XM_PUSH_KEY}\""
            buildConfigField "String", "XM_PUSH_ID", "\"${rootProject.ext.IK_XM_PUSH_ID}\""
            //听云配置
            buildConfigField "String", "TY_APP_KEY", "\"${rootProject.ext.IK_TY_APP_KEY}\""
            buildConfigField "String", "APP_NAME_CN", "\"爱客云\""
        }
    }

    flavorDimensions "versionCode"

    sourceSets {
        main {
            jniLibs.srcDir(['libs'])
        }

        ikyun {
            res.srcDirs += ['src/main/res-ikh5']
        }
    }

    dexOptions {
        preDexLibraries = false
    }

    lintOptions {
        abortOnError = false
    }

    greendao {
        schemaVersion 5
        daoPackage 'com.lixiaoyun.aike.greendaoDB'
        targetGenDir 'src/main/java'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            if (variant.buildType.name == "release") {
                outputFileName = variant.productFlavors[0].name + "_" + variant.versionName + "_" + variant.versionCode + "_release.apk"
            } else if (variant.buildType.name == "debug") {
                outputFileName = variant.productFlavors[0].name + "_" + variant.versionName + "_" + variant.versionCode + "_debug.apk"
            }
        }
    }
}

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    //阿里云
    implementation files('libs/aliyun-log-android-sdk-2.1.0-javadoc.jar')
    implementation files('libs/aliyun-log-android-sdk-2.1.0-sources.jar')
    implementation files('libs/aliyun-log-sdk-android-2.1.0.jar')

    //x5内核
    implementation files('libs/tbs_sdk_thirdapp_v4.3.0.1148_43697_sharewithdownloadwithfile_withoutGame_obfs_20190805_175505.jar')

    //小米推送
    implementation files('libs/MiPush_SDK_Client_3_7_0.jar')

    //个推推送
    implementation 'com.getui:sdk:2.13.3.0'

    //华为推送
    implementation 'com.huawei.android.hms:base:2.6.3.306'
    implementation 'com.huawei.android.hms:push:2.6.3.306'

    //七牛云
    implementation 'com.qiniu:qiniu-android-sdk:7.5.2'

    //权限获取 https://github.com/yanzhenjie/AndPermission
    implementation 'com.yanzhenjie:permission:2.0.3'

    //noinspection GradleDependency PhotoView https://github.com/chrisbanes/PhotoView/ 由于AndoridX和android.support库不能共存，暂用2.1.4
    implementation 'com.github.chrisbanes:PhotoView:2.3.0'

    //二维码扫码 https://github.com/bingoogolapple/BGAQRCode-Android
    implementation 'cn.bingoogolapple:bga-qrcode-zbar:1.3.6'

    //仿ios弹框 https://github.com/Kaopiz/KProgressHUD
    implementation 'com.kaopiz:kprogresshud:1.2.0'

    //事件总线 https://github.com/greenrobot/EventBus
    implementation 'org.greenrobot:eventbus:3.1.1'

    //glide
    implementation 'com.github.bumptech.glide:glide:4.10.0'

    //gson
    implementation 'com.google.code.gson:gson:2.8.6'

    //fastjson
    implementation 'com.alibaba:fastjson:1.2.62'

    //日志工具 https://github.com/orhanobut/logger
    implementation 'com.orhanobut:logger:2.2.0'

    //rx https://github.com/ReactiveX/RxKotlin
    implementation 'io.reactivex.rxjava2:rxkotlin:2.4.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'

    //retrofit2
    implementation 'com.squareup.retrofit2:retrofit:2.7.1'
    implementation 'com.squareup.retrofit2:converter-gson:2.7.1'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.7.1'

    //noinspection GradleDependency
    implementation 'com.squareup.okhttp3:logging-interceptor:4.3.0'

    //友盟
    implementation 'com.umeng.umsdk:common:1.5.4'
    implementation 'com.umeng.umsdk:utdid:1.1.5.3'
    implementation 'com.umeng.umsdk:share-core:6.9.4'
    implementation 'com.umeng.umsdk:share-board:6.9.4'
    implementation 'com.umeng.umsdk:share-wx:6.9.4'
    implementation 'com.umeng.umsdk:share-sina:6.9.4'
    implementation 'com.umeng.umsdk:share-qq:6.9.4'

    //https://github.com/yanzhenjie/Album 适用于Android平台的专辑和图库。
    implementation 'com.yanzhenjie:album:2.1.3'

    //https://github.com/zetbaitsu/Compressor 图片压缩
    implementation 'id.zelory:compressor:2.1.0'

    //高德定位
    implementation 'com.amap.api:location:4.8.0'

    //数据库
    implementation 'org.greenrobot:greendao:3.2.2'
    implementation 'io.github.yuweiguocn:GreenDaoUpgradeHelper:v2.2.1'

    //听云
    implementation 'com.networkbench.newlens.agent.android:nbs.newlens.agent:2.14.2'
    implementation 'com.networkbench.newlens.agent.android:nbs.newlens.nativecrash:1.0.0'

    //神策
    implementation 'com.sensorsdata.analytics.android:SensorsAnalyticsSDK:3.2.9'
}
